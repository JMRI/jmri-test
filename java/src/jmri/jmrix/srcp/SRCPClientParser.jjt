/* This is a parser grammar for the responses to a client in 
the SRCP protocol written for code generation with javacc */

options {
  STATIC=false;
  MULTI=true;
  VISITOR=true;
  TRACK_TOKENS=true;
}

PARSER_BEGIN(SRCPClientParser)

package jmri.jmrix.srcp.parser;

import org.slf4j.Logger;
import org.slf4j.LoggerFactory;

/* 
 * This file defines a JavaTree/JavaCC parser implementation for
 * the SRCP protocol responses to the client.
 * 
 * @author Paul Bender Copyright (C) 2011
 * @version $Revision$
 */

// This is generated code.  DO NOT ATTEMPT TO FIX ANY FINDBUGS WARNINGS
@edu.umd.cs.findbugs.annotations.SuppressWarnings(value="NM_METHOD_NAMING_CONVENTION")

public class SRCPClientParser {
  
   static Logger log = LoggerFactory.getLogger(SRCPClientParser.class.getName());

  }

PARSER_END(SRCPClientParser)

// The Tokenizer specification begins here:
<IN_REASON>
MORE :
{
  <REASON: ~[]>
}

<DEFAULT,IN_REASON>
TOKEN :
{
  < EOL :(["\n","\r"]){2} >:DEFAULT
}

<DEFAULT>
SKIP:
{
     <RETURN: "\r">
   | <NEWLINE: "\n">
   | <SPACE: " ">
}

<DEFAULT>
TOKEN :
{
  < ZEROADDR: ("0")( ["0"-"9"] )* >
| < NONZEROADDR: ["1"-"9"](["0"-"9"])* >
| < TIMESTAMP: (["0"-"9"])+((".")(["0"-"9"])+)? >
| < JMRITURNOUT: ["a"-"z","A"-"Z"] "T" (["0"-"9"])+ >
| < JMRISENSOR: ["a"-"z","A"-"Z"] "S" (["0"-"9"])+ >
| < JMRILIGHT: ["a"-"z","A"-"Z"] "L" (["0"-"9"])+ >
| < ONOFF: (<ON>|<OFF>) >
| < #ON: <O><N>> 
| < #OFF: <O>(<F>){2} >
| < PROTOCOL: (<A>|<F>|<L>|<P>|<S>|<N>|<M>|<Z>) >
| < DRIVEMODE: ("="|"0"|"1"|"2") >
| < JULDATE: (["1"-"9"]){2,4}["0"-"3"]["0"-"9"]["0"-"9"]>
| < HOUR: (["0"-"2"])?["0"-"9"]>
| < MINSEC: ["0"-"5"]["0"-"9"]>
| < DELAY: ("-")?(["0"-"9"])+>

// Device types
| < FB: <F><B> >
| < GA: <G><A> >
| < GL: <G><L> >
| < GM: <G><M> >
| < SM: <S><M> >
| < LOCK: <L><O><C><K> >
| < TIME: <T><I><M><E>>
| < POWER: <P><O><W><E><R>>
| < SESSION: <S><E><S><S><I><O><N>>
| < DESCRIPTION: <D><E><S><C><R><I><P><T><I><O><N> >
| < SERVER: <S><E><R><V><E><R>>

// Constants and commands used for handshake mode only.
| < SRCP: <S><R><C><P>>
| < GO: <G><O>>
| < VERSION: (["0"-"9"])+"."(["0"-"9"])+"."(["0"-"9"])+>
| <PROTOCOLLITTERAL: <P><R><O><T><O><C><O><L>>
| <CONNECTIONMODELITTERAL: <C><O><N><N><E><C><T><I><O><N><M><O><D><E>>

// commands 
| < GET: <G><E><T> >
| < SET: <S><E><T> >
| < CHECK: <C><H><E><C><K> >
| < INIT: <I><N><I><T> >
| < TERM: <T><E><R><M> >
| < WAIT: <W><A><I><T> >
| < VERIFY: <V><E><R><I><F><Y>>
| < RESET: <R><E><S><E><T>>

// Client return codes
| < INFO: <I><N><F><O>>:IN_REASON
| < OK: <O><K>>:IN_REASON
| < ERROR: <E><R><R><O><R>>:IN_REASON
| < ERRORCODE: (["1"-"5"])(["0"-"9"])(["0"-"9"])>

| < COMMAND: <C><O><M><M><A><N><D>>

//programming modes
| <CVBIT: <C><V><B><I><T>>
| <CV: <C><V>>
| <REG: <R><E><G>>

| <#A:["a","A"]>
| <#B:["b","B"]>
| <#C:["c","C"]>
| <#D:["d","D"]>
| <#E:["e","E"]>
| <#F:["f","F"]>
| <#G:["g","G"]>
| <#H:["h","H"]>
| <#I:["i","I"]>
| <#J:["j","J"]>
| <#K:["k","K"]>
| <#L:["l","L"]>
| <#M:["m","M"]>
| <#N:["n","N"]>
| <#O:["o","O"]>
| <#P:["p","P"]>
| <#Q:["q","Q"]>
| <#R:["r","R"]>
| <#S:["s","S"]>
| <#T:["t","T"]>
| <#U:["u","U"]>
| <#V:["v","V"]>
| <#W:["w","W"]>
| <#X:["x","X"]>
| <#Y:["y","Y"]>
| <#Z:["z","Z"]>
}

// Lexical specifications begin here

SimpleNode command():{}
{
  // This is a simple list of the possible commands.  
  // They are broken down further below.
  ( LOOKAHEAD(4) go() | get() | set() | check() | wait_cmd() | init() |
                 term() | reset() | verify()) <EOL>
  {
    log.debug("Command Production");
    //jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode handshakeresponse():{SimpleNode t; SimpleNode s;}
{
  // this should match any valid response to the client in handshake
  // mode.
  t=timestamp() ( s=serviceversion() | s=connectionmode() | s=inforesponse() )
  {
    jjtThis.jjtSetValue(s.jjtGetValue());
    log.debug("Handshake Response Production: "+jjtThis.jjtGetValue());
    return jjtThis;
  }
}

SimpleNode timestamp():{ Token t;}
{
   (t=<TIMESTAMP> | t=<NONZEROADDR> | t=<ZEROADDR> )
  {
    jjtThis.jjtSetValue(t.image);
    log.debug("Timestamp Production: "+jjtThis.jjtGetValue());
    return jjtThis;
  }
}

SimpleNode serviceversion():{Token t; Token s;}
{
  ( t=<SRCP> (s=<VERSION>) <EOL> )
  {
    log.debug("Service Version Production");
    jjtThis.jjtSetValue(t.image +" " + s.image);
    return jjtThis;
  }
}

SimpleNode connectionmode():{Token t;}
{
  (( t=<COMMAND> | t=<INFO> ) <EOL> )
  {
    log.debug("Connection Mode Production");
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode inforesponse():{SimpleNode t; Token s; Token r;}
{
  t=address() (s=<INFO> | s=<OK> | s=<ERROR>) r=<EOL>
  {
    jjtThis.jjtSetValue(t.jjtGetValue() + " " +s.image + r.image);
    log.debug("Information Response Production: " + jjtThis.jjtGetValue() );
    return jjtThis;
  }
}

SimpleNode commandresponse():{SimpleNode t; SimpleNode s; }
{
  // this should match any valid response to the client
  /*((<ADDR> | <ERRORCODE>) bus() ( ga() address() port() zeroone() |
				 power() onoff() )) <EOL>
  <TIMESTAMP> s=inforesponse() bus() ( ga() address() port() zeroone() |
                         power() onoff() ) <EOL>*/
  // this should match any valid response to the client in 
  // either command mode or info mode.
  t=timestamp() ( s=inforesponse() ) 
  {
    log.debug("Command Mode Response Production");
    jjtThis.jjtSetValue(s.jjtGetValue() );

    return jjtThis;
  }
}

SimpleNode cvno():{SimpleNode s;}
{
  ( s=nonzeroaddress())
  {
    log.debug("CV Number Production "+ s.jjtGetValue());
    jjtThis.jjtSetValue(s.jjtGetValue());
    return jjtThis;
  }
}


SimpleNode bus():{SimpleNode s;}
{
  (s=zeroaddress() | s=nonzeroaddress())
  {
    log.debug("BUS Production "+ s.jjtGetValue());
    jjtThis.jjtSetValue(s.jjtGetValue());
    return jjtThis;
  }
}

SimpleNode address():{SimpleNode s;}
{
  (s=zeroaddress() | s=nonzeroaddress())
  {
    log.debug("Address Production "+ s.jjtGetValue());
    jjtThis.jjtSetValue(s.jjtGetValue());
    return jjtThis;
  }
}
SimpleNode zeroaddress():{Token t;}
{
  t=<ZEROADDR>
  {
    //Zero Address allows a leading zero
    log.debug("ZeroAddress Production "+ t.image);
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode nonzeroaddress():{Token t;}
{
  t=<NONZEROADDR>
  {
    //NonZero Address requires a value  >=1, without
    //a leading zero
    log.debug("NonZeroAddress Production "+ t.image);
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode port():{SimpleNode s;}
{
  (s=zeroaddress() | s=nonzeroaddress())
  {
    log.debug("PORT Production "+ s.jjtGetValue());
    jjtThis.jjtSetValue(s.jjtGetValue());
    return jjtThis;
  }
}


SimpleNode devicegroup():{Token t;}
{
  ( <FB> | <GA> | <GL> | <SM> | <TIME> | <SESSION> | <SERVER> | 
    <LOCK> | <DESCRIPTION> )
  {
    log.debug("DeviceGroup Production");
    //jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}


SimpleNode go():{Token t;}
{
  t=<GO> 
  {
     log.debug("GO Production");
    jjtThis.jjtSetValue(t.image);
     return jjtThis;
  }
}

SimpleNode get():{Token t;}
{
  t=<GET> bus() ( fb() address() | 
                ga() address() port() | 
                gl() address() | 
                sm() address() | 
                time() | 
                power() | 
                server() | 
                session () | 
                lock() devicegroup() address() | 
                description() [ devicegroup() address() ] )

  {
     log.debug("GET Production");
    jjtThis.jjtSetValue(t.image);
     return jjtThis;
  }
}

SimpleNode set():{Token t;}
{
  t=<SET> ( ( bus() ( gl() | 
                sm() | 
                ga() address() port() zeroone() delay() | 
                fb() address() port() | 
                time() | 
                power() onoff() | 
                lock() ) ) |
	<PROTOCOLLITTERAL> serviceversion() | 
	<CONNECTIONMODELITTERAL> connectionmode() ) 
  {
    log.debug("SET Production");
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode term():{Token t;}
{
  t=<TERM> bus()  ( fb() | gl() | sm() | power() | server() | lock() )
  {
    jjtThis.jjtSetValue(t.image);
   return jjtThis;
  }
}

SimpleNode check():{Token t;}
{
  t=<CHECK> bus() ( gl() | sm() | ga() address() port() zeroone() (delay()|timeout()) | 
                 fb() | time() | power() onoff() | lock() )
  {
    log.debug("Check Production");
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode wait_cmd():{Token t;}
{
  t=<WAIT> bus() ( fb() address() port() (delay()|timeout())|time() )

  {
     log.debug("WAIT Production");
    jjtThis.jjtSetValue(t.image);
     return jjtThis;
  }
}

SimpleNode init():{Token t;}
{
  t=<INIT> bus() ( fb() | ga() | gl() | sm() | time() | power() )
  {
     log.debug("INIT Production");
    jjtThis.jjtSetValue(t.image);
     return jjtThis;
  }
}

SimpleNode reset():{Token t;}
{
  t=<RESET> bus() <SERVER>
  {
     log.debug("RESET Production");
    jjtThis.jjtSetValue(t.image);
     return jjtThis;
  }
}

SimpleNode verify():{Token t;}
{
  t=<VERIFY> bus() sm()
  {
     log.debug("RESET Production");
    jjtThis.jjtSetValue(t.image);
     return jjtThis;
  }
}

SimpleNode gl():{Token t;}
{
  t=<GL>
  {
    jjtThis.jjtSetValue(t.image);
    return jjtThis;}
}

SimpleNode sm():{Token t;}
{
  t=<SM>
  {
    jjtThis.jjtSetValue(t.image);
    return jjtThis;}
}

SimpleNode ga():{Token t;}
{
  t=<GA>
  { 
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode fb():{Token t;}
{
  t=<FB>
  {
    jjtThis.jjtSetValue(t.image);
    return jjtThis;}
}

SimpleNode time():{Token t;}
{
  t=<TIME> 
  {
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode power():{Token t;}
{
  t=<POWER> 
  {
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode server():{Token t;}
{
  t=<SERVER> 
  {
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode session():{Token t;}
{
  t=<SESSION> 
  {
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode lock():{Token t;}
{
  t=<LOCK> 
  {
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode description():{Token t;}
{
  t=<DESCRIPTION> 
  {
    jjtThis.jjtSetValue(t.image);
    return jjtThis;
  }
}

SimpleNode comment():{}
{
  //<COMMENT>
  {return jjtThis;}
}

SimpleNode onoff():{Token t;}
{
  t=<ONOFF>
  {
     log.debug("OnOff Production " +t.image);
     jjtThis.jjtSetValue(t.image);
     return jjtThis;
  }
}

SimpleNode zeroone():{Token t;}
{
  (zeroaddress() | nonzeroaddress())
  {
     log.debug("ZeroOne Production ");
     return jjtThis;
  }
}

SimpleNode delay():{Token t;}
{
  // time timeout is a non-zero integer.  It may be negative.
  t=<DELAY>
  {
     log.debug("Delay Production " +t.image);
     return jjtThis;
  }
}

SimpleNode timeout():{Token t;}
{
  // the timeout is a non-negative integer.
  address()
  {
     log.debug("Timeout Production ");
     return jjtThis;
  }
}

// service mode programming modes

SimpleNode progmode():{SimpleNode s;}
{
  // programming mode has 3 possibilities
  ( s=cvbit() | s=cv() | s=reg() )
  {
     log.debug("Programming Mode Production ");
     return jjtThis;
  }
}


SimpleNode cv():{}
{
  <CV>
  {return jjtThis;}
}

SimpleNode cvbit():{}
{
  <CVBIT>
  {return jjtThis;}
}

SimpleNode reg():{}
{
  <REG>
  {return jjtThis;}
}

